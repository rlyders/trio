10 Clear: DefInt A-Z
20 KEY 1, Chr$(27) + "LIST" + Chr$(13): KEY 2, Chr$(27) + "LIST ": KEY 3, Chr$(27) + "RUN" + Chr$(13): KEY 4, Chr$(27) + "RUN  " + Chr$(34): KEY 5, Chr$(27) + "SAVE " + Chr$(34) + "T": KEY 6, Chr$(27) + "FILES" + Chr$(13): KEY 7, Chr$(27) + "LOAD " + Chr$(34): KEY 8, Chr$(27) + "RUN "
30 KEY 9, Chr$(27) + "RENUM" + Chr$(13): KEY 10, Chr$(27) + "SCREEN 0,1" + Chr$(13)
40 Rem
50 Rem     TRIO  - Graphics
60 On Error GoTo 5730: Cls: Dim O$(20), OO$(20), OO(20)
70 Screen 1: KEY Off: BCOLOR = 1: PALET = 0: Color BCOLOR, PALET
80 PI! = 22 / 7: C = 2: B = 3: X = 20: Y = 69: GoSub 350: C = 3: B = 1: X = 25: Y = 64: GoSub 350
90 Locate 17, 12, 0: Print "A Game Created By"
100 Locate , 6, 0: Print "Richard Jason Lyders-Gustafson"
110 Locate , 16, 0: Print "With Ideas"
120 Locate , 19, 0: Print "From"
130 Locate , 13, 0: Print "Olaf Schabirosky"
131 Print: Print: Color 1: Locate , 13, 0: Print "[PRESS ANY KEY]";
140 Def Seg = 0: Poke 1050, Peek(1052): While Len(InKey$) = 0: Wend
181 Screen 0, 1: Width 40: Color , 0, 1: Locate , , 0: Cls: O$ = "Instructions-": O$(1) = "no": O$(2) = "yes": O = 2: GoSub 8500
290 If AA = 2 Then GoSub 4460: GoTo 680 Else If AA = 1 Then 680 Else GoTo 10
350 '
360 Circle (X + 45, Y), 10, B, 3 * PI! / 2, PI! / 2
370 Circle (X - 5, Y), 10, B, PI! / 2, 3 * PI! / 2
380 Line (X - 5, Y - 9)-(X + 45, Y - 9), B
390 Line (X - 5, Y + 9)-(X + 10, Y + 9), B
400 Line (X + 30, Y + 9)-(X + 45, Y + 9), B
410 Circle (X + 20, Y + 50), 10, B, PI!, 0
420 Line (X + 30, Y + 50)-(X + 30, Y + 9), B
430 Line (X + 10, Y + 50)-(X + 10, Y + 9), B
440 Paint (X + 20, Y + 50), C, B
450 X = X + 80
460 Circle (X, Y), 10, B, PI! / 2, PI!
470 Circle (X, Y + 50), 10, B, PI!, 0
480 Line (X - 10, Y)-(X - 10, Y + 50), B
490 Line (X + 1, Y - 8)-(X + 30, Y - 8), B
500 Line (X + 10, Y + 45)-(X + 10, Y + 50), B
510 Circle (X + 27, Y + 13), 25, B, 3 * PI! / 2, PI! / 2, 5 / 6
520 Circle (X + 10, Y + 13), 7, B, -3 * PI! / 2, -PI! / 2
530 Circle (X + 29, Y + 50), 10, B, 3 * PI! / 2.5, 2 * PI! / 6.7
540 Line (X + 37, Y + 45)-(X + 27, Y + 35), B
550 Line (X + 22, Y + 56)-(X + 10, Y + 44), B
560 Paint (X, Y), C, B
570 X = X + 80
580 Circle (X, Y + 50), 10, B, PI!, 0
590 Circle (X, Y), 10, B, 0, PI!
600 Line (X - 10, Y)-(X - 10, Y + 50), B
610 Line (X + 10, Y)-(X + 10, Y + 50), B
620 Paint (X, Y), C, B
630 X = X + 70: Y = Y + 25
640 Circle (X, Y), 7, B
650 Circle (X, Y), 40, B
660 Paint (X, Y + 10), C, B
670 Return
680 '##################################
690 '##         main program         ##
700 '##################################
710 Clear: On Error GoTo 5730
720 Dim GN$(22), UNK$(10), KY$(11), A(7, 7), S(40, 3), A$(9): WW = 4
721 Erase O$, OO$, OO
730 Screen 0, 1: Color , 0, 1: Cls: ZZ = 8
740 KEY Off: Width 40: Locate , , 0
750 Rem
760 Rem  load scores/names
770 Dim SCORE(11), NAMES$(11)
780 Open "I", #1, "T.F": For N = 0 To 9: Input #1, NAMES$(N), SCORE(N): Next N: Close #1
790 Screen 0, 1: Color 0, 0, 1: Cls
800 CNAME = 10
810 GoSub 7460
811 If SG Then GoSub 7280: If SG Then GoSub 7510: If SG Then GoSub 7250: GoSub 7680: GoSub 7430: GoTo 1300
820 If ESC Then ESC = 0: GoTo 181 Else Color , 0, 1: Cls
840 NAMES$(CNAME) = ""
850 Y1 = 12: X1 = 20: A = 14: B = 0: C = 14: D = 1: LK = 10: OO$ = LASTNAME$: O$ = "Your Name|": GoSub 20000: SG = 0: If ESC Then ESC = 0: GoSub 7460: If SG Then 811 Else GoTo 181
851 NAMES$(CNAME) = KO$: If NAMES$(CNAME) = Space$(10) Then NAMES$(CNAME) = Chr$(175) + Space$(6) + Chr$(174)
860 Color , 0: Locate Y1, X1 - 10: Print Space$(20)
1300 Color 14, 0
1310 Locate 11, 11: Print " You will now have a"
1320 Locate , 10: Print "  20-second countdown"
1330 Locate , 9: Print "before your game begins..."
1340 For W = 1 To WW
    1342 A$ = Time$
    1345 While A$ = Time$
        1350 If Len(InKey$) Then 1370
    1355 Wend
1360 NEXT W
1370 SCREEN 0,1:COLOR 0,0:CLS
1380 SCRN=1:GOSUB 5840
1390 COLOR 3,0
1400 LOCATE 4,32,0:PRINT USING "\      \";NAMES$(CNAME)
1410 L=13
1420 FOR N=9 TO 0 STEP -1:IF SCORE(N)=0 THEN SWAP NAMES$(CNAME),NAMES$(N):SWAP SCORE(CNAME),SCORE(N):CNAME=N
1430 NEXT N
1440 FOR S=0 TO 9:L=L+1:COLOR 1-2*(CNAME=S),0:LOCATE L,22,0:PRINT USING"\        \########";NAMES$(S),SCORE(S):NEXT S
1450 IF SG=0 THEN 1580
1460 COLOR 3,0
1470 LOCATE 5,32,0:PRINT USING "########";SCORE(CNAME)
1480 LOCATE ,32,0:PRINT USING "########";CNSR
1490 LOCATE ,32,0:PRINT USING "########";WNSR
1500 ROUNDS=ROUNDS+1
1510 LOCATE ,32,0:PRINT USING "########";TRYS/ROUNDS
1520 IF TRYS=0 THEN AVGSPT=0:GOTO 1540
1530 AVGSPT=(SECONDS/TRYS)
1540 LOCATE ,32,0:PRINT USING "########";AVGSPT
1550 COLOR 7,1:LOCATE 3,2,0:PRINT USING "###";ROUNDS
1560 LOCATE 3,18,0:PRINT USING "###";MNSR
1570 LOCATE 25,5:PRINT USING "####";TIME;
1580 REM
1590 REM assign a-array
1600 IF SG THEN 1650
1610 NEWGAME=1
1620 MNSR=0
1630 TIME=250
1640 SCORE(10)=0
1650 GRIDCOLOR=4
1660 RANDOMIZE TIMER
1670 COLOR GRIDCOLOR,0,1
1680 GV=6
1690 FOR GY=0 TO 6
1700 GH=2
1710 FOR GX=0 TO 6
1720 A(GX,GY)=INT(RND*9)+1
1730 LOCATE GV,GH,0
1740 GH=GH+3
1750 PRINT USING "#";A(GX,GY)
1760 NEXT GX:GV=GV+2:NEXT GY
1770 PLAY "mb o1 a45 > a45 > a45 >a45"
1780 REM
1790 REM create answer
1800 IF SG THEN 1830
1810 ROUNDS=0
1820 ROUNDS=ROUNDS+1
1830 RANDOMIZE TIMER
1840 AX1=INT(RND*7)
1850 AY1=INT(RND*7)
1860 AYA=1
1870 AXA=INT(RND*3)-1:IF AYA=0 THEN 1900
1880 IF AX1>4  AND AXA=1 THEN 1870
1890 IF AX1<2  AND AXA=-1 THEN 1870
1900 AYA=INT(RND*3)-1
1910 IF AY1>4  AND AYA=1 THEN 1900
1920 IF AY1<2  AND AYA=-1 THEN 1900
1930 IF AXA=0 AND AYA=0 THEN 1860
1940 A1=A(AX1,AY1)
1950 AX2=AX1+AXA:AY2=AY1+AYA
1960 A2=A(AX2,AY2)
1970 AX3=AX2+AXA:AY3=AY2+AYA
1980 A3=A(AX3,AY3)
1990 AAA=INT(RND*3)-1
2000 IF AAA=0 THEN 1990
2010 ANSWR=A1*A2+(AAA*A3)
2020 IF NEWGAME THEN 2470
2030 REM
2040 REM set up score-screen
2050 COLOR 3,0
2060 IF TRYS=0 THEN AVGSPT=0:GOTO 2080
2070 AVGSPT=(SECONDS/TRYS)
2080 IF AVGSPT=0 THEN SCORE(CNAME)=0 :GOTO 2140
2090 IF INCORRECT THEN CSCORE=(-1)*((1/3)*(CSECONDS)):GOTO 2120
2100 IF CORRECT THEN CSCORE=1000/((3/4)*(CSECONDS)) ELSE CSCORE=0:GOTO 2120
2110 IF CSCORE<50 THEN CSCORE=50
2120 CORRECT=0
2130 SCORE(CNAME)=SCORE(CNAME)+CSCORE
2140 LOCATE 5,32,0:PRINT USING "########";SCORE(CNAME)
2150 LOCATE ,32,0:PRINT USING "########";CNSR
2160 LOCATE ,32,0:PRINT USING "########";WNSR
2170 LOCATE ,32,0:PRINT USING "########";TRYS/ROUNDS
2180 LOCATE ,32,0:PRINT USING "########";AVGSPT
2190 FOR D=1 TO 10
2200 IF CNAME=0 THEN 2270
2210 IF SCORE(CNAME)>SCORE(CNAME-1) OR SCORE(CNAME-1)=0 THEN 2220 ELSE 2260
2220 SWAP SCORE(CNAME),SCORE(CNAME-1)
2230 SWAP NAMES$(CNAME),NAMES$(CNAME-1)
2240 CNAME=CNAME-1
2250 GOTO 2310
2260 IF CNAME=10 THEN 2310
2270 IF SCORE(CNAME)<SCORE(CNAME+1) THEN 2280 ELSE 2310
2280 SWAP SCORE(CNAME),SCORE(CNAME+1)
2290 SWAP NAMES$(CNAME),NAMES$(CNAME+1)
2300 CNAME=CNAME+1
2310 NEXT D
2320 L=13
2330 FOR S=0 TO 9:L=L+1
2340 IF S=CNAME THEN COLOR 3,0 ELSE COLOR 1,0
2350 LOCATE L,22,0:PRINT USING"\        \########";NAMES$(S),SCORE(S)
2370 NEXT S
2380 IF LEN(UNK$(UNK))=2 AND RIGHT$(UNK$(UNK),1)=CHR$(82) THEN 2390 ELSE 2470
2390 COLOR GRIDCOLOR,0
2400 LOCATE LY1,LX1,0:PRINT USING "#";CA1
2410 LOCATE LY2,LX2,0:PRINT USING "#";CA2
2420 LOCATE LY3,LX3,0:PRINT USING "#";CA3
2430 COLOR 0,0
2440 LOCATE 3,8,0 :PRINT"        "
2450 REM
2460 REM accept info
2470 IF SG THEN 2520
2480 IF NEWGAME THEN 2520
2490 LOCATE 3,11,0
2500 COLOR 0,0
2510 PRINT"  "
2520 UNK=1:UL=9
2530 IF SG THEN SG=0:GOTO 2550
2540 IF NEWGAME THEN 2550 ELSE 2620
2550 DEF SEG=0:POKE 1050,PEEK(1052):COLOR 15,0:COUNT=20
2560 ON TIMER(1) GOSUB 7170:TIME$="0":A$=TIME$:WHILE A$=TIME$:WEND:TIMER ON
2570 IF COUNT>0 AND LEN(INKEY$)=0 THEN 2570 ELSE TIMER OFF:ON TIMER(1) GOSUB 7130
2580 TIME$="0":A$=TIME$:WHILE A$=TIME$:WEND:LOCATE 3,10:PRINT"  "
2590 LOCATE 2,7:PRINT SPC(9)
2600 PLAY "MF o3 C16"
2610 TIME$="0":A$=TIME$:WHILE A$=TIME$:WEND
2620 NEWGAME=0
2630 COLOR 14,1:LOCATE 3,11,0
2640 PLAY "mF ms o2 d64 f64 >b64"
2650 IF ANSWR>9 OR ANSWR<0 THEN 2670
2660 PRINT USING"#";ANSWR:GOTO 2680
2670 PRINT USING"##";ANSWR
2680 COLOR 7,1:LOCATE 3,2,0:PRINT USING "###";ROUNDS
2690 LOCATE 3,18,0:PRINT USING "###";MNSR
2700 IF INCORRECT THEN INCORRECT=0:GOTO 2720
2710 TIME$="1":TIMER ON
2720 IF LEN(INKEY$) THEN 2720
2730 '
2740 COLOR 7,1:LOCATE 25,11,0:PRINT USING"&";RIGHT$(TIME$,5);MID$(STR$(TIMER),6,2);
2750 CSECONDS=(60*(VAL(MID$(TIME$,4,2)))+(VAL(RIGHT$(TIME$,2))))
2760 LOCATE 25,5,0:IF TIME=>20 THEN COLOR 7,1 ELSE COLOR 14,1
2770 PRINT USING "####";TIME;
2780 UNK$(UNK)=INKEY$:IF LEN(UNK$(UNK))=0 THEN 2740
2790 IF VAL(UNK$(UNK)) THEN 2880
2800 IF LEN(UNK$(UNK))=2 AND RIGHT$(UNK$(UNK),1)=CHR$(83) THEN COLOR 14,0:LOCATE 20,9,0:PRINT CHR$(176)" "CHR$(176) " "CHR$(176):LOCATE 20,9:PRINT"      ":UNK=1:UL=9:PLAY "mb ms o1 g64 g64 g64":GOTO 2730
2810 IF LEN(UNK$(UNK))=2 AND RIGHT$(UNK$(UNK),1)=CHR$(82) THEN COLOR 0,0:LOCATE 20,2,0:PRINT"                   ":GOTO 3620
2820 IF LEN(UNK$(UNK))=2 AND RIGHT$(UNK$(UNK),1)=CHR$(79) THEN GOSUB 8320:IF ED THEN END ELSE 181
2830 IF UNK$(UNK)=CHR$(8) THEN PLAY"mb ms o1 g64":UNK=UNK-1:UL=UL-2 ELSE 2870
2840 IF UNK<1 THEN UNK=1
2850 IF UL<9 THEN UL=9
2860 COLOR 14,0:LOCATE 20,UL,0:PRINT CHR$(176);:LOCATE ,UL,0:PRINT" ":GOTO 2720
2870 PLAY"mb mn o0 c32 c32":GOTO 2720
2880 PLAY "mb mn o3 c40"
2890 COLOR 14,1
2900 LOCATE 20,UL,0
2910 PRINT UNK$(UNK):UL=UL+2:UNK=UNK+1
2920 ON UNK-1 GOTO 2930,2940,2950
2930 ANSR1=VAL(UNK$(1)):GOTO 2730
2940 ANSR2=VAL(UNK$(2)):GOTO 2730
2950 ANSR3=VAL(UNK$(3))
2960 SECONDS=SECONDS+CSECONDS
2970 COLOR 0,0
2980 LOCATE 3,11:PRINT"  "
2990 LOCATE 20,9:PRINT"      "
3000 TIMER OFF
3010 REM
3020 REM find asnr1
3030 FOR Y=0 TO 6
3040 FOR X=0 TO 6
3050 UNK$=INKEY$
3060 IF LEN(UNK$)=2 AND RIGHT$(UNK$,1)=CHR$(83) THEN 3070 ELSE 3170
3070 PLAY "mb o1 g25 g25 g10"
3080 UNK=1
3090 UL=9
3100 COLOR 14,1:LOCATE 3,11
3110 IF ANSWR>9 OR ANSWR<1 THEN 3130 ELSE 3120
3120 PRINT USING "#";ANSWR: GOTO 3140
3130 PRINT USING "##";ANSWR
3140 COLOR 14,0:LOCATE 20,9:PRINT CHR$(176)" "CHR$(176)" "CHR$(176)
3150 COLOR 0,0:LOCATE 20,9:PRINT"     "
3160 TIMER ON:GOTO 2740
3170 IF ANSR1=A(X,Y) THEN 3210
3180 NEXT X,Y:GOTO 3390
3190 REM
3200 REM  find ANSR2
3210 FOR YA=-1 TO 1
3220 Y2=Y+YA
3230 FOR XA=-1 TO 1
3240 IF XA=0 AND YA=0 THEN 3280
3250 X2=X+XA
3260 IF X2>6 OR X2<0 OR Y2>6 OR Y2<0 THEN 3280
3270 IF ANSR2=A(X2,Y2) THEN 3320
3280 NEXT XA,YA
3290 GOTO 3180
3300 REM
3310 REM find ANSR3
3320 XA3=(-1)*(X-X2):YA3=(-1)*(Y-Y2)
3330 X3=X2+XA3:Y3=Y2+YA3
3340 IF X3>6 OR X3<0 OR Y3>6 OR Y3<0 THEN 3360
3350 IF ANSR3=A(X3,Y3) THEN 3500
3360 GOTO 3280
3370 REM
3380 REM  If incorrect
3390 TIMER OFF:PLAY "mb ms o2 e16 d16 <<a"
3400 INCORRECT=1
3410 WNSR=WNSR+1:TRYS=TRYS+1
3420 COLOR 14,1:LOCATE 20,2,0
3430 PRINT USING "&";"Incorrect,try again"
3440 FOR W=1 TO WW/2:A$=TIME$:WHILE A$=TIME$:WEND:NEXT W
3450 SRCH$=INKEY$
3460 COLOR 0,0
3470 LOCATE 20,2,0:PRINT"                   "
3480 IF SRCH$=CHR$(27) THEN 3870
3490 TIMER ON:GOTO 2050
3500  REM
3510  REM If correct
3520 IF (ANSWR=(ANSR1*ANSR2)+ANSR3) OR (ANSWR=(ANSR1*ANSR2)-ANSR3) THEN 3530 ELSE 3390
3530 TIMER OFF:PLAY "mb ms o2 c24 f24 >g"
3540 CORRECT=1
3550 CNSR=CNSR+1:TRYS=TRYS+1
3560 COLOR 14,1:LOCATE 20,2,0
3570 PRINT"     CORRECT!      "
3580 FOR W=1 TO WW/2:A$=TIME$:WHILE A$=TIME$:WEND:NEXT W
3590 COLOR 0,0
3600 LOCATE 20,2,0:PRINT"                   "
3610 TIMER ON:GOTO 1820
3620 REM
3630 REM show answer(answr)
3640 TIMER OFF:PLAY "mb ms o2 c64 d64 e64"
3650 COLOR 11,1
3660 MNSR=MNSR+1
3670 LOCATE 3,8,0
3680 IF AAA=-1 THEN 3720
3690 IF ANSWR>9 OR ANSWR<0 THEN 3710
3700 PRINT USING "#_*#_+#_=#";A1,A2,A3,ANSWR:GOTO 3750
3710 PRINT USING "#_*#_+#_=##";A1,A2,A3,ANSWR:GOTO 3750
3720 IF ANSWR>9 OR ANSWR<0 THEN 3740
3730 PRINT USING "#_*#_-#_=#";A1,A2,A3,ANSWR:GOTO 3750
3740 PRINT USING "#_*#_-#_=##";A1,A2,A3,ANSWR
3750 LY1=6+(AY1*2):LX1=2+(AX1*3)
3760 LY2=6+(AY2*2):LX2=2+(AX2*3)
3770 LY3=6+(AY3*2):LX3=2+(AX3*3)
3780 COLOR 11,0
3790 LOCATE LY1,LX1,0:PRINT USING "#";A1
3800 LOCATE LY2,LX2,0:PRINT USING "#";A2
3810 LOCATE LY3,LX3,0:PRINT USING "#";A3
3820 CA1=A1:CA2=A2:CA3=A3
3830 FOR W=1 TO WW:A$=TIME$:WHILE A$=TIME$:WEND:NEXT W
3840 TIMER ON:GOTO 1820
3850 REM
3860 REM search
3870 TIMER OFF:COLOR 10,1:LOCATE 2,7,0
3880 IF ANSWR>9 OR ANSWR<0 THEN 3900
3890 PRINT USING "SEARCH_[#_]";ANSWR:GOTO 3910
3900 PRINT USING "SEARCH_[##_]";ANSWR
3910 FOR COLUMN1=0 TO 6
3920 FOR ROW1=0 TO 6
3930 FOR ACOLUMN=-1 TO 1
3940 FOR AROW=-1 TO 1
3950 IF ROW1<2 AND AROW=-1 OR ROW1>4 AND AROW=1 OR COLUMN1<2 AND ACOLUMN=-1 OR COLUMN1>4 AND ACOLUMN=1 OR ACOLUMN=0 AND AROW=0 THEN 4000
3960 Z1=A(ROW1,COLUMN1)
3970 Z2=A(ROW1+AROW,COLUMN1+ACOLUMN)
3980 Z3=A(ROW1+AROW+AROW,COLUMN1+ACOLUMN+ACOLUMN)
3990 IF Z1*Z2+Z3=ANSWR OR Z1*Z2-Z3=ANSWR THEN 4070
4000 NEXT AROW,ACOLUMN,ROW1,COLUMN1
4010 COLOR 10,1:LOCATE 3,8,0
4020 PRINT"Finished"
4030 FOR W=1 TO WW:A$=TIME$:WHILE A$=TIME$:WEND:NEXT W
4040 COLOR 0,0:LOCATE 2,6,0:PRINT"           "
4050 COLOR 0,0:LOCATE 3,8,0:PRINT"         "
4060 TIMER ON:GOTO 1820
4070 Q=Q+1
4080 S(Q,1)=Z1
4090 S(Q,2)=Z2
4100 S(Q,3)=Z3
4110 PLAY "mb o1 c30"
4120 COLOR 10,0
4130 LOCATE 3,8,0
4140 IF Z1*Z2+Z3=ANSWR THEN 4180
4150 IF ANSWR>9 OR ANSWR<0 THEN 4170
4160 PRINT USING "#_*#_-#_=#";S(Q,1),S(Q,2),S(Q,3),ANSWR :GOTO 4210
4170 PRINT USING "#_*#_-#_=##";S(Q,1),S(Q,2),S(Q,3),ANSWR :GOTO 4210
4180 IF ANSWR>9 OR ANSWR<0 THEN 4200
4190 PRINT USING "#_*#_+#_=#";S(Q,1),S(Q,2),S(Q,3),ANSWR :GOTO 4210
4200 PRINT USING "#_*#_+#_=##";S(Q,1),S(Q,2),S(Q,3),ANSWR
4210 SY1=6+(COLUMN1*2):SX1=2+(ROW1*3)
4220 SY2=6+((ACOLUMN+COLUMN1)*2):SX2=2+((AROW+ROW1)*3)
4230 SY3=6+((ACOLUMN+ACOLUMN+COLUMN1)*2):SX3=2+((AROW+AROW+ROW1)*3)
4240 COLOR 10,0
4250 PLAY "mb o3 c30 d30 e30"
4260 LOCATE SY1,SX1,0:PRINT USING "#";Z1
4270 LOCATE SY2,SX2,0:PRINT USING "#";Z2
4280 LOCATE SY3,SX3,0:PRINT USING "#";Z3
4290 FOR W=1 TO WW:A$=TIME$:WHILE A$=TIME$:WEND:NEXT W
4300 COLOR GRIDCOLOR,0
4310 LOCATE SY1,SX1,0:PRINT USING "#";Z1
4320 LOCATE SY2,SX2,0:PRINT USING "#";Z2
4330 LOCATE SY3,SX3,0:PRINT USING "#";Z3
4340 COLOR 0,0
4350 LOCATE 3,8,0:PRINT"        "
4360 GOTO 4000
4370 REM
4380 REM  Save scores/names
4390 OPEN "O",#1,"T.F"
4400 FOR N=0 TO 9:IF SCORE(N)<=0 THEN SCORE(N)=0:NAMES$(N)=SPACE$(8):GOTO 4420
4410 IF LEFT$(NAMES$(N),8)=CHR$(175)+SPACE$(6)+CHR$(174) THEN NAMES$(N)=SPACE$(8)
4420 WRITE #1,NAMES$(N),SCORE(N):NEXT N:CLOSE #1:GOTO 710
4430 REM
4440 REM  Save  game
4450 OPEN "O",#2,"TT.F":WRITE #2,MNSR,ROUND,SCORE(CNAME),TIME,TRYS,SECONDS,NAMES$(CNAME),CNSR,WNSR:CLOSE #2:RETURN
4460  '##################################
4470  '##        TRIO INSTRUCTIONS     ##
4480  '##################################
4490 WIDTH 80
4500 SCREEN 0,1:COLOR 0,0,1:CLS
4510 LOCATE ,,0
4520 SCRN=2:GOSUB 5840
4530 H=45
4540 COLOR 7,0:LOCATE 10,51
4550 PRINT"The object of ";:COLOR 14:PRINT"TRIO ";:COLOR 7:PRINT":"
4560 PRINT:LOCATE ,H+1:COLOR 8:PRINT"Complete as many rounds of play"
4570 LOCATE ,H+3:PRINT"as possible within the given"
4580 LOCATE ,H+9:PRINT"250-count time."
4590 IF LEN(INKEY$)=0 THEN 4590
4600 COLOR 7,0:LOCATE 8,48
4610 PRINT"The object of each round"
4620 PRINT:LOCATE ,H:COLOR 8:PRINT"Locate three numbers(`";
4630 COLOR 7:PRINT"GUESSES";
4640 COLOR 8:PRINT"')"
4650 LOCATE ,H+2:PRINT"in a straight line where:"
4660 COLOR 7:LOCATE ,H-2:PRINT"GUESS 1 x GUESS 2 + GUESS 3 = ANSWER"
4670 COLOR 8:LOCATE ,H-2:PRINT"                  or                "
4680 COLOR 7:LOCATE ,H-2:PRINT"GUESS 1 x GUESS 2 - GUESS 3 = ANSWER"
4690 COLOR 8:PRINT:PRINT:LOCATE ,H-1:PRINT"(`";
4700 COLOR 7:PRINT"ANSWER";
4710 COLOR 8:PRINT"' is yellow on blue print)"
4720 COLOR 7,1:LOCATE 3,2:PRINT USING "###";1
4730 COLOR 14,0:LOCATE 3,13:PRINT CHR$(17)CHR$(205);
4740 COLOR ,1:PRINT" ANSWER ";
4750 IF LEN(INKEY$) THEN 4750
4760 IF LEN(INKEY$) THEN 4770 ELSE 4760
4770 COLOR 7,0:LOCATE 4,42:PRINT"             A sample round:          "
4780 COLOR 8:PRINT:LOCATE ,42:PRINT "   1)The `";
4790 COLOR 7:PRINT"ANSWER";
4800 COLOR 8:PRINT"' is displayed.       "
4810 LOCATE ,42:PRINT"     In this example it is `";
4820 COLOR 14,1:PRINT"7";
4830 COLOR 8,0:PRINT"'.       "
4840 LOCATE ,42:PRINT"   2)Search for your three guesses. "
4850 LOCATE ,42:PRINT"          In this example:          "
4860 LOCATE ,42:PRINT"          `";
4870 COLOR 7:PRINT"GUESS 1";
4880 COLOR 8:PRINT"' is `";
4890 COLOR 12:PRINT"6";
4900 COLOR 8:PRINT"'            "
4910 LOCATE ,42:PRINT"          `";
4920 COLOR 7:PRINT"GUESS 2";
4930 COLOR 8:PRINT"' is `";
4940 COLOR 12:PRINT"2";
4950 COLOR 8:PRINT"'            "
4960 LOCATE ,42:PRINT"          `";
4970 COLOR 7:PRINT"GUESS 3";
4980 COLOR 8:PRINT"' is `";
4990 COLOR 12:PRINT"5";
5000 COLOR 8:PRINT"'            "
5010 COLOR 12:LOCATE ,42:PRINT"            6 ";
5020 COLOR 8:PRINT"x ";
5030 COLOR 12:PRINT"2 ";
5040 COLOR 8 :PRINT"- ";
5050 COLOR 12:PRINT"5 ";
5060 COLOR 8:PRINT"= ";
5070 COLOR 14,1:PRINT"7";:COLOR ,0:PRINT "             "
5080 COLOR 8,0:LOCATE ,42:PRINT"   3)Enter your guesses in order of:  "
5090 LOCATE ,42:PRINT"     `";
5100 COLOR 7:PRINT"GUESS 1";
5110 COLOR 8:PRINT"', `";
5120 COLOR 7:PRINT"GUESS 2";
5130 COLOR 8:PRINT"', `";
5140 COLOR 7:PRINT"GUESS 3";
5150 COLOR 8:PRINT"'. "
5160 LOCATE ,42:PRINT"   4)The computer will check your     "
5170 LOCATE ,42:PRINT"     set of guesses.                  "
5180 LOCATE ,42:PRINT"   5)If correct, a new round begins.  "
5190 LOCATE ,42:PRINT"   6)If incorrect, the same round     "
5200 LOCATE ,42:PRINT"     continues.                       "
5210 COLOR 12,0:LOCATE 6,8:PRINT"5"
5220 LOCATE 8,5:PRINT"2"
5230 LOCATE 10,2:PRINT"6"
5240 IF LEN(INKEY$) THEN 5240
5250 IF LEN(INKEY$) THEN 5260 ELSE 5250
5260 COLOR 7:LOCATE 3,42:PRINT"            SPECIAL KEYS              "
5270 COLOR 7:LOCATE ,42:PRINT"                                      "
5280 LOCATE 5,42:PRINT"  `DEL':";
5290 COLOR 8:PRINT"Erases guesses                "
5300 COLOR 7:LOCATE ,42:PRINT"  `BACKSPACE':";
5310 COLOR 8:PRINT"Erases last guess       "
5320 COLOR 7:LOCATE ,42:PRINT"  `FN'-`END':";
5330 COLOR 8:PRINT"Ends & saves game        "
5340 COLOR 7:LOCATE ,42:PRINT"  `INS':";
5350 COLOR 8:PRINT"Ends current round,           "
5360 LOCATE ,42:PRINT"        displays correct guesses      "
5370 COLOR 7:LOCATE ,42:PRINT"  `ESC':";
5380 COLOR 8:PRINT"Searches for correct guesses, "
5390 LOCATE ,42:PRINT"        if pressed while displaying   "
5400 LOCATE ,42:PRINT"        `";
5410 COLOR 14,1:PRINT"Incorrect,try again";
5420 COLOR 8,0:PRINT"'         "
5430 COLOR 7:LOCATE ,42:PRINT"                                      "
5440 LOCATE ,42:PRINT"            GAME FORMAT               "
5450 LOCATE ,42:PRINT"                                      "
5460 LOCATE ,42:PRINT"   RND:";
5470 COLOR 8:PRINT"Current round                  "
5480 COLOR 7:LOCATE ,42:PRINT"   INS:";
5490 COLOR 8:PRINT"# of times `";
5500 COLOR 7:PRINT"INS";
5510 COLOR 8:PRINT"'-key is used   "
5520 COLOR 7:LOCATE ,42:PRINT"   AVGTRYS/R:";
5530 COLOR 8:PRINT"Average trys per round   "
5540 COLOR 7:LOCATE ,42:PRINT"   AVGSECS/T:";
5550 COLOR 8:PRINT"Average seconds per try  "
5560 COLOR 7:LOCATE ,42:PRINT"   CORRECT:";
5570 COLOR 8:PRINT"# of correct guesses       "
5580 COLOR 7:LOCATE ,42:PRINT"   INCORRECT:";
5590 COLOR 8:PRINT"# of incorrect guesses   "
5600 COLOR 7:LOCATE ,42:PRINT"   TIME:";
5610 COLOR 8:PRINT"Remaining game time         "
5620 COLOR 7:LOCATE ,42:PRINT"   TIMER:";
5630 COLOR 8:PRINT"Elapsed round time           ";
5640 IF LEN(INKEY$) THEN 5640
5650 IF LEN(INKEY$) THEN 5660 ELSE 5650
5660 FOR ZZ=3 TO 24:LOCATE ZZ,42:PRINT SPACE$(38);:NEXT ZZ
5670 COLOR 8:LOCATE 10,42:PRINT"     Next, once you press any key,"
5680 LOCATE ,42:PRINT"             your game of"
5690 LOCATE ,42:COLOR 14:PRINT"                 TRIO"
5700 LOCATE ,42:COLOR 8:PRINT"              will begin."
5710 DEF SEG=0:POKE 1050,PEEK(1052)
5720 IF LEN(INKEY$) THEN RETURN ELSE 5720
5730 COLOR 14,1:SCREEN 0,1,0:CLS:COLOR 14,4:LOCATE 12,15
5740 IF ERR=53  THEN PRINT"Insert Game Disk":GOTO 5830
5750 IF ERR=7  THEN PRINT"Note enough memory":GOTO 5830
5760 IF ERR=57 OR ERR=71 THEN PRINT"Check Diskdrive":GOTO 5830
5770 IF ERR=63  THEN PRINT"Disk is full":GOTO 5830
5780 IF ERR=67  THEN PRINT"Too many files":GOTO 5830
5790 IF ERR=68  THEN PRINT"No diskdrive":GOTO 5830
5800 IF ERR=70  THEN PRINT"Disk is write protected":GOTO 5830
5810 LOCATE 12,7:PRINT"Severe Error...Reset Game:"
5820 IF LEN(INKEY$) THEN PRINT ERL;ERR:END ELSE PLAY"mf b64 p32":GOTO 5820
5830 IF LEN(INKEY$) THEN COLOR 14,1:CLS:RESUME ELSE PLAY"mf b64 p32":GOTO 5830
5840 SCREEN 0,1:COLOR 0,0:CLS
5850  '
5860  '@@@@ GAME SCREEN @@@@
5870  '
5880 COLOR 6
5890 IF SCRN=2 THEN 5930
5900 COLOR 6
5910 LOCATE 25,40:PRINT CHR$(188);
5920 LOCATE 24,40:PRINT CHR$(186);
5930 IF SCRN=1 THEN 6000
5940 COLOR 8
5950 LOCATE 25,80:PRINT CHR$(188);
5960 LOCATE 24,80:PRINT CHR$(186);
5970 COLOR 6
5980 LOCATE 25,40:PRINT CHR$(188);
5990 LOCATE 24,40:PRINT CHR$(186);
6000 COLOR 6
6010 FOR LNE=2 TO 39
6020 IF LNE<3 THEN LOCATE 25,LNE:PRINT CHR$(205);
6030 IF LNE>19 AND LNE<21 THEN LOCATE 25,LNE:PRINT CHR$(205);
6040 IF LNE>3 AND LNE<19 THEN LOCATE 23,LNE:PRINT CHR$(205);
6050 IF LNE>5 AND LNE<17 THEN LOCATE 21,LNE:PRINT CHR$(205);
6060 LOCATE 1,LNE:PRINT CHR$(205);
6070 IF LNE>21 THEN 6080 ELSE 6120
6080 LOCATE 3,LNE:PRINT CHR$(205);
6090 LOCATE 10,LNE:PRINT CHR$(205);
6100 LOCATE 12,LNE:PRINT CHR$(205);
6110 LOCATE 25,LNE:PRINT CHR$(205);
6120 IF LNE<21 THEN 6130 ELSE 6140
6130 LOCATE 4,LNE:PRINT CHR$(205);
6140 NEXT LNE
6150 FOR LNE=2 TO 24
6160 LOCATE LNE,1:PRINT CHR$(186);
6170 LOCATE LNE,21:PRINT CHR$(186);
6180 IF LNE<24 THEN LOCATE LNE,40:PRINT CHR$(186);
6190 NEXT LNE
6200 LOCATE 1,1:PRINT CHR$(201);
6210 LOCATE 1,5:PRINT CHR$(209);
6220 LOCATE 1,17:PRINT CHR$(209);
6230 LOCATE 1,21:PRINT CHR$(203);
6240 LOCATE 1,40:PRINT CHR$(187);
6250 LOCATE 3,40:PRINT CHR$(185);
6260 LOCATE 3,21:PRINT CHR$(204);
6270 LOCATE 4,1:PRINT CHR$(204);
6280 LOCATE 4,21:PRINT CHR$(185);
6290 LOCATE 4,5:PRINT CHR$(207);
6300 LOCATE 4,17:PRINT CHR$(207);
6310 LOCATE 24,3:PRINT CHR$(186);SPC(15);CHR$(186);
6320 LOCATE 22,5:PRINT CHR$(186);SPC(11);CHR$(186);
6330 LOCATE 2,5:PRINT CHR$(179);SPC(11);CHR$(179);
6340 LOCATE 3,5:PRINT CHR$(179);SPC(11);CHR$(179);
6350 LOCATE 10,21:PRINT CHR$(204);
6360 LOCATE 10,40:PRINT CHR$(185);
6370 LOCATE 12,21:PRINT CHR$(204);
6380 LOCATE 12,40:PRINT CHR$(185);
6390 LOCATE 21,5:PRINT CHR$(201);
6400 LOCATE 21,17:PRINT CHR$(187);
6410 LOCATE 23,3:PRINT CHR$(201);
6420 LOCATE 23,19:PRINT CHR$(187);
6430 LOCATE 23,5:PRINT CHR$(202);
6440 LOCATE 23,17:PRINT CHR$(202);
6450 LOCATE 25,19:PRINT CHR$(200);
6460 LOCATE 25,3:PRINT CHR$(188);
6470 LOCATE 25,1:PRINT CHR$(200);
6480 LOCATE 25,21:PRINT CHR$(202);
6490 COLOR  7
6500 LOCATE 2,2:PRINT"RND"
6510 LOCATE 2,18:PRINT"INS"
6520 LOCATE 24,5:PRINT"TIME";
6530 LOCATE 24,12:PRINT"TIMER";
6540 COLOR 14
6550 LOCATE 22,8:PRINT"T R I O"
6560 COLOR 7,1
6570 LOCATE 3,2:PRINT USING"###";0      
6580 LOCATE 3,18:PRINT USING"###";0     
6590 LOCATE 25,5:PRINT USING"####";250;
6600 LOCATE 25,11:PRINT"00:00.0";
6610 COLOR 3,0
6620 LOCATE 2,25:PRINT"YOUR SCORES"
6630 LOCATE 4,27:PRINT"NAME:"
6640 LOCATE 5,26:PRINT"SCORE:       0"
6650 LOCATE 6,24:PRINT"CORRECT:       0"
6660 LOCATE 7,22:PRINT"INCORRECT:       0"
6670 LOCATE 8,22:PRINT"AVGTRYS/R:       0"
6680 LOCATE 9,22:PRINT"AVGSECS/T:       0"
6690 COLOR 1
6700 LOCATE 11,25:PRINT"HIGH SCORES"
6710 FOR LNE=14 TO 23
6720 LOCATE LNE,39:PRINT"0";
6730 NEXT LNE
6740 IF SCRN=1 THEN COLOR 15 ELSE 6780
6750 LOCATE 2,7:PRINT"COUNTDOWN"
6760 LOCATE 3,10:PRINT"20"
6770 RETURN
6780 '
6790 '@@@@ INSTRUCTION SCREEN @@@@
6800 '
6810 COLOR 8
6820 FOR LNE=42 TO 79
6830 LOCATE 1,LNE:PRINT CHR$(205);
6840 LOCATE 25,LNE:PRINT CHR$(205);
6850 NEXT LNE
6860 FOR LNE=2 TO 24
6870 LOCATE LNE,41:PRINT CHR$(186);
6880 IF LNE<24 THEN LOCATE LNE,80:PRINT CHR$(186);
6890 NEXT LNE
6900 LOCATE 1,41:PRINT CHR$(201);
6910 LOCATE 1,80:PRINT CHR$(187);
6920 LOCATE 25,41:PRINT CHR$(200);
6930 COLOR 7
6940 LOCATE 1,55:PRINT"INSTRUCTIONS"
6950 LOCATE 25,54:PRINT"PRESS ANY KEY";
6960 GRIDCOLOR=4
6970 RANDOMIZE TIMER
6980 COLOR GRIDCOLOR,0,1
6990 GV=6
7000 FOR GY=0 TO 6
7010 GH=2
7020 FOR GX=0 TO 6
7030 IF GX=2 AND GY=0 THEN A(GX,GY)=5:GOTO 7070
7040 IF GX=1 AND GY=1 THEN A(GX,GY)=2:GOTO 7070
7050 IF GX=0 AND GY=2 THEN A(GX,GY)=6:GOTO 7070
7060 A(GX,GY)=INT(RND*9)+1
7070 LOCATE GV,GH,0
7080 GH=GH+3
7090 PRINT USING "#";A(GX,GY)
7100 NEXT GX:GV=GV+2:NEXT GY
7110 COLOR 14,1:LOCATE 3,11:PRINT"7"
7120 RETURN
7130 TIME=TIME-1:IF TIME=0 THEN PLAY"mb ml o4 c2":LOCATE 25,5:COLOR 14,1:PRINT USING"####";TIME;:LOCATE 20,7,0:COLOR 3,0:PRINT "GAME OVER":NEWGAME=0:LASTNAME$=NAMES$(CNAME):GOTO 4370
7140 IF TIME=>20 THEN 7160
7150 IF PLY=0 THEN PLY=1:PLAY"MB MS O4 C64" ELSE PLY=0
7160 RETURN
7170 COUNT=COUNT-1:LOCATE 3,10,0:PRINT USING"##";COUNT
7180 IF COUNT>3 THEN PLAY"MF ms O3 A64" ELSE PLAY"mF o3 a32"
7190 RETURN
7200 COLOR 14,0:CLS:END
7210 REM
7220 REM ****SAVE GAME IN - GN$****
7230 OPEN "O",#1,GN$+".TF":WRITE #1,MNSR,ROUND,SCORE(CNAME),TIME,TRYS,SECONDS,NAMES$(CNAME),CNSR,WNSR:CLOSE #1:RETURN
7240 REM
7250 REM ****LOAD GAME IN - GN$****
7260 OPEN "I",#1,GN$+".TF":INPUT #1,MNSR,ROUND,SCORE(CNAME),TIME,TRYS,SECONDS,NAMES$(CNAME),CNSR,WNSR:CLOSE #1:RETURN
7270 REM
7280 REM
7290 REM LOAD SAVED GAME?
7291 O$="Load a Game-":O$(1)="no":O$(2)="yes":O=2:GOSUB 8500:SG=AA-1:COLOR 0,0,1:CLS:IF AA=0 THEN SG=0:RETURN 181 ELSE RETURN
7420 REM
7430 REM
7440 REM save file names
7450 OPEN "O",#1,"T.FF":FOR A=1 TO GN:WRITE #1,GN$(A):NEXT A:CLOSE #1:RETURN
7460 REM
7470 REM load file names
7480 OPEN "I",#1,"T.FF":FOR A=1 TO 20
7490 IF EOF(1) THEN GN=A-1 ELSE INPUT #1,GN$(A):NEXT A
7500 CLOSE #1:IF GN=0 THEN SG=0:RETURN ELSE SG=1:RETURN
7510 REM
7520 REM choose from saved game list
7530 POKE 1050,PEEK(1052):IF GN=0 THEN SG=0:COLOR ,0:CLS:RETURN
7540 COLOR 8,0:LOCATE 2,12:PRINT"�����������ͻ"
7550 FOR A=1 TO 20:LOCATE 2+A,12:PRINT"�           �";:NEXT A
7560 LOCATE 23,12:PRINT"�����������ͼ";
7570 X1=13:COLOR ZZ,0:FOR A=1 TO GN:LOCATE A+2,X1:PRINT USING"##)";A;:PRINT GN$(A);:NEXT:A=1:LA=1
7580 COLOR ZZ,0:LOCATE LA+2,X1:PRINT USING"##)";LA;:PRINT GN$(LA)+SPACE$(8-LEN(GN$(LA)));:COLOR 14,1:LOCATE A+2,X1:PRINT USING"##)";A;:PRINT GN$(A)+SPACE$(8-LEN(GN$(A)));
7590 A$=INKEY$:ON LEN(A$)+1 GOTO 7590,7600,7620
7600 IF A$=CHR$(13) THEN 7670
7610 IF A$=CHR$(27) THEN SG=0:COLOR ,0:CLS:RETURN 810 ELSE 7590
7620 A$=RIGHT$(A$,1):IF A$=CHR$(80) THEN LA=A:A=A+1:GOTO 7650
7630 IF A$=CHR$(83) THEN KILL GN$(A)+".TF":FOR C=A TO GN:GN$(C)=GN$(C+1):NEXT C:GN=GN-1:GOSUB 7430:GOTO 7510
7640 IF A$=CHR$(72) THEN LA=A:A=A-1 ELSE 7590
7650 IF A>GN THEN A=1 ELSE IF A<1 THEN A=GN
7660 GOTO 7580
7670 GN$=GN$(A):COLOR ,0:CLS:RETURN
7680 REM
7690 REM ERASE GAME FILE
7700 FOR A=1 TO 20:IF GN$<>GN$(A) THEN NEXT A
7710 FOR A=A TO 20:GN$(A)=GN$(A+1):NEXT A:GN=GN-1:KILL GN$+".TF":RETURN
7720 REM
7730 REM ACCEPT INFORMATION
7731 A=14:B=0:C=14:D=1:LK=8:OO$=GN$:O$="File Name|":X1=12:Y1=20:KY$="":GOSUB 20000:IF ESC THEN RETURN
8100 KY$=KO$:IF KY$=SPACE$(8) THEN GOSUB 8121:RETURN ELSE K$=KY$:GOSUB 8210:GOSUB 8170:GOSUB 8250
8110 IF K$<>KY$ THEN GN$=KY$:GOTO 7731 ELSE GOSUB 8130:GOSUB 8121
8120 IF KY$<>"" THEN GN=GN+1:GN$(GN)=KY$:GN$=KY$:GOSUB 7430:RETURN ELSE 7731
8121 LOCATE Y1,X1-10:COLOR 7,0:PRINT SPACE$(18);:RETURN
8130 REM
8140 REM SEARCH FOR DUPLICATE FILES
8150 FOR A=1 TO GN:IF KY$=GN$(A) THEN KY$=""
8160 NEXT A:IF KY$="" THEN LOCATE 20,2:COLOR 14,4:PRINT "DUPLICATE FILE NAME":FOR W=1 TO 3:A$=TIME$:WHILE A$=TIME$:WEND:NEXT W:COLOR ,0:LOCATE 20,2:PRINT SPACE$(19):RETURN ELSE RETURN
8170 REM
8180 REM ERASE SPACES IN FILE NAME
8190 FOR A=1 TO 8:IF LEFT$(KY$,1)=CHR$(32) THEN KY$=RIGHT$(KY$,8-A):NEXT A
8200 RETURN
8210 REM
8220 REM capitalize file names
8230 FOR A=1 TO 8:A$=RIGHT$(KY$,A):IF ASC(A$)=>97 AND ASC(A$)<=122 THEN KY$=LEFT$(KY$,(8-A))+CHR$(ASC(A$)-32)+RIGHT$(A$,A-1)
8240 NEXT A:RETURN
8250 REM
8260 REM create a valid file name
8270 FOR A=1 TO 8:AA=ASC(RIGHT$(KY$,A))
8280 IF AA=33 OR (AA>34 AND AA<42) OR AA=45 OR (AA>46 AND AA<58) OR (AA>63 AND AA<91) OR (AA>93 AND AA<97) OR AA=123 OR AA=125 THEN 8310
8290 IF AA=32 THEN IF RIGHT$(KY$,A)=SPACE$(A) THEN 8310
8300 KY$=LEFT$(KY$,8-A)+"_"+RIGHT$(KY$,A-1)
8310 NEXT A:RETURN
8320 REM
8330 REM END/SAVE/NEW GAME OPTIONS
8331 A=7:B=0:C=14:D=1:ED=0:X1=10:Y1=20:O$(1)="save":O$(2)="end":O$(3)="new":O=3:GOSUB 8530
8440 COLOR 0,0:LOCATE Y1,X1-8:PRINT SPACE$(19)
8480 IF AA=0 THEN RETURN 2720
8490 IF AA=1 THEN GOSUB 7460:GOSUB 7720:IF GN$<>"" AND ESC=0 THEN GOSUB 7210:GOTO 8320 ELSE ESC=0:GOTO 8320 ELSE IF AA=2 THEN COLOR 8,0:CLS:ED=1:RETURN ELSE ED=0:RETURN
8500 REM
8510 REM menus
8520 Y1=12:X1=20:A=7:B=0:C=14:D=1
8530 REM O$=Text:O$()=Options 1-5:O=# of options:a=forgnd:b=bakgnd:c=fore-high:d=bkgnd-high:x1=cntr x pos:y1=y pos:aa=current option #
8540 DEF SEG=0:POKE 1050,PEEK(1052):AA=1
8550 XX=LEN(O$):FOR F=1 TO O:XX=XX+LEN(O$(F))+3:NEXT F:XX=X1-INT((XX-3)/2)
8560 COLOR A,B:LOCATE Y1,XX:PRINT O$+"[";:FOR AA=1 TO O-1:GOSUB 8740:PRINT O$(AA)+" | ";:NEXT:GOSUB 8740:PRINT O$(O)+"]";:AA=1
8570 XXX=XX+LEN(O$)+1:LOCATE Y1,XXX:FOR F=1 TO O:IF AA=F THEN COLOR C,D:PRINT OO$(F); ELSE IF OO(F) THEN COLOR ZZ,B:PRINT O$(F) ELSE COLOR A,B:PRINT O$(F);
8580 XXX=XXX+LEN(O$(F))+3:LOCATE Y1,XXX:NEXT
8590 A$=INKEY$:ON LEN(A$)+1 GOTO 8590,8600,8630
8600 IF A$=CHR$(13) THEN 8710
8610 IF A$=CHR$(27) THEN AA=0:GOTO 8710
8620 FOR F=1 TO O:IF ASC(A$)=ASC(O$(F)) OR ASC(A$)=ASC(OO$(F)) THEN AA=F:GOTO 8710 ELSE NEXT:GOTO 8590
8630 A$=RIGHT$(A$,1):IF A$<>CHR$(77) THEN  8660
8640 IF AA=O THEN AA=1 ELSE AA=AA+1
8650 IF OO(AA) THEN 8640 ELSE 8570
8660 IF A$<>CHR$(75) THEN 8690
8670 IF AA=1 THEN AA=O ELSE AA=AA-1
8680 IF OO(AA) THEN 8670 ELSE 8570
8690 IF ASC(A$)=>120 AND ASC(A$)<=129 THEN WW=(ASC(A$)-120):GOTO 8590
8700 IF A$=CHR$(79) THEN GOSUB 8320:IF ED THEN END ELSE 10 ELSE 8590
8710 XXX=XX+LEN(O$)+1:LOCATE Y1,XXX:FOR F=1 TO O:IF AA=F THEN COLOR C,D:PRINT OO$(F); ELSE IF OO(F) THEN COLOR ZZ,B:PRINT O$(F) ELSE COLOR A,B:PRINT O$(F);
8720 XXX=XXX+LEN(O$(F))+3:LOCATE Y1,XXX:NEXT
8730 FOR F=1 TO O:O$(F)="":OO(F)=0:NEXT:O$="":RETURN
8740 REM
8750 REM Capitalizing
8760 OO$(AA)=O$(AA):OO=LEN(OO$(AA)):FOR F=1 TO OO:A$=RIGHT$(OO$(AA),F):IF ASC(A$)=>97 AND ASC(A$)<=122 THEN OO$(AA)=LEFT$(OO$(AA),(OO-F))+CHR$(ASC(A$)-32)+RIGHT$(A$,F-1)
8770 NEXT:RETURN
20000 REM ENTER **************
20001 REM A=7:B=0:C=14:D=1:Y1=13:X1=20:O$="Your name|":LK=10
20010 ESC=0:LK=LK-1:COLOR C,B:LOCATE Y1,X1-LEN(O$),0:PRINT O$;:COLOR C,D:PRINT USING"\"+SPACE$(LK-1)+"\";OO$
20020 LOCATE Y1,X1,0
20030 KY=0
20040 IF KY>LK THEN KY=0
20050 IF INS THEN LOCATE ,,,3,7 ELSE LOCATE ,,,7,0
20060 KY$(KY)=INKEY$
20070 LOCATE Y1,X1+KY,1
20080 IF LEN(KY$(KY)) THEN 20090 ELSE 20040
20090 LOCATE ,,0
20100 IF KY$(KY)=CHR$(13) THEN 20220
20110 IF KY$(KY)=CHR$(9) THEN 20040
20120 IF KY$(KY)=CHR$(27) THEN ESC=1:KY$=SPACE$(LK+1):INS=0:COLOR ,0:LOCATE Y1,X1-LEN(O$):PRINT SPACE$(LEN(O$)+LK+1):KY=0:SG=0:O$="":RETURN
20130 IF LEN(KY$(KY))<>2 OR RIGHT$(KY$(KY),1)<>CHR$(83) THEN 20150
20140 INS=0:FOR I=KY TO LK:LOCATE Y1,X1+I:PRINT CHR$(SCREEN(Y1,X1+1+I)):NEXT:GOTO 20040
20150 IF LEN(KY$(KY))=2 AND RIGHT$(KY$(KY),1)=CHR$(82) THEN IF INS THEN INS=0:GOTO 20040 ELSE INS=1:GOTO 20040
20160 IF LEN(KY$(KY))=2 AND RIGHT$(KY$(KY),1)=CHR$(75) THEN KY=KY-1:INS=0:IF KY<0 THEN KY=LK:GOTO 20040 ELSE 20040
20170 IF LEN(KY$(KY))=1 AND KY$(KY)=CHR$(8) THEN KY=KY-1:IF KY<0 THEN KY=0:GOTO 20140 ELSE 20140
20180 IF LEN(KY$(KY))=2 AND RIGHT$(KY$(KY),1)=CHR$(77) THEN INS=0:KY=KY+1:GOTO 20040
20190 IF LEN(KY$(KY))<>1 THEN 20040 ELSE IF INS=0 THEN 20210 ELSE FOR I=LK TO KY STEP -1:LOCATE Y1,X1+I:PRINT CHR$(SCREEN(Y1,X1+I-1)):NEXT
20200 LOCATE Y1,X1+KY
20210 PRINT KY$(KY):KY=KY+1:GOTO 20040
20220 KO$="":FOR KY=0 TO LK:KO$=KO$+CHR$(SCREEN(Y1,X1+KY)):NEXT:OO$="":O$="":LK=0:RETURN
